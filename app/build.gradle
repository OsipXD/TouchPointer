apply plugin: 'com.android.application'

android {
    signingConfigs {
        TouchPointer {
            keyAlias 'TouchPointer'
            keyPassword 'IlikeT0drink'
            storeFile file('D:/Developing/Android/EndlessCode.jks')
            storePassword 'IlikeT0drink'
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "ru.endlesscode.touchpointer"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = [] //disable automatic ndk-build call
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.TouchPointer
        }
    }
    productFlavors {
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'org.jetbrains:annotations-java5:15.0'
    compile 'com.android.support:support-v4:25.1.0'
    testCompile 'junit:junit:4.3'
    testCompile 'org.mockito:mockito-core:2.0.42-beta'
    testCompile 'org.powermock:powermock-module-junit4-legacy:1.6.6'
    testCompile 'org.powermock:powermock-api-mockito2:1.6.6'
    androidTestCompile 'com.android.support:support-annotations:25.1.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}

task('compileNative') {
    def $ndkBuildScript = System.properties['os.name'].toLowerCase().contains('windows') ? 'ndk-build.cmd' : 'ndk-build'
    def $ndkProjectRootFolder = 'src/main'
    def $ndkDirPropertyName = 'ndk.dir'

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def $ndkDir = properties.getProperty($ndkDirPropertyName)

    if ($ndkDir == null)
        throw new RuntimeException("Property 'ndk.dir' not found. Please specify it. \n" +
                " It must be something like this in your local.properties file \n" +
                " ndk.dir=path/to/your/ndk")

    exec {
        executable = $ndkDir + '/' + $ndkBuildScript
        args = ["NDK_PROJECT_PATH=" + $ndkProjectRootFolder]
    }
}

task nativeLibsToJar(type: Zip, description: 'create a jar archive of the native libs', dependsOn: 'compileNative') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    extension 'jar'
    from fileTree(dir: 'jniLibs', include: '**/*.so')
    into 'lib/'
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}
